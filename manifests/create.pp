# @summary Create a new collection
#
# Create a collection and the unerlying `collection::iterator` resource which powers it.
#
# @example
#   collections::create { 'collection-name':
#   }
#
#   collections::create { 'Operations for all users':
#     target   => 'users::all',
#     defaults => {
#       parent => '/home'
#     }
#   }
#
# @param [String[1]] target
#   The name of this collection. You must pass this target name to all resources that
#   act upon or work with this collection.
#
# @param [Hash[String, Any]] defaults
#   Parameters that will be used as defaults for any resources generated by this collection.
#   When a resource is created, these will be passed in as default parameters.
#
# @param [Array[Any]] initial_items
#   A set of items to begine the collection with. This should be an array of data that could
#   be added one at a time with `collection::append`.
#
define collections::create (
  String[1] $target = $title,
  Hash[String, Any] $defaults = {},
  Array[Any] $initial_items = []
) {
  collections::iterator { $target:
    items     => $initial_items,
    actions   => [],
    executors => [],
  }

  Collections::Append <| target == $target |> -> Collections::Commit[$target]
}
